// Add kotlin support to gradle
buildscript {
    ext.kotlin_version = '1.0.1'
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.7"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.moowork.node" version "0.12"
}

node {
    download = true
}

group = "net.antcenter"
version = "0.1"


apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

task bowerInstall(type: NodeTask) {
    script = file('node_modules/bower/bin/bower')
    args = ["--config.storage.cache=${gradle.getGradleUserHomeDir()}/caches/bower/cache",
            "--config.storage.packages=${gradle.getGradleUserHomeDir()}/caches/bower/packages",
            "--config.storage.registry=${gradle.getGradleUserHomeDir()}/caches/bower/registry",
            'install']
    inputs.files file('bower.json')
    outputs.files file('bower_components')
    dependsOn npmInstall
}

task nodeImportCesium(type: Sync) {
    from 'node_modules/cesium/Build/Cesium'
    into 'resources/main/webroot/vendor/Cesium'
    dependsOn bowerInstall
}

tasks.getByName('processResources').dependsOn nodeImportCesium

task bowerImport(type: Sync) {
    from 'bower_components'
    into 'resources/main/webroot/vendor/bower_components'
    dependsOn bowerInstall
}

tasks.getByName('processResources').dependsOn bowerImport

apply from: 'buildscripts/mixedlang.gradle'
apply from: 'buildscripts/fatjar.gradle'

repositories {
    mavenCentral()
    repositories { maven { url "http://dl.bintray.com/kyonifer/maven" } }

}

mainClassName = "MainKt"

dependencies {
    // Depend on the std libs of all our languages
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile "com.beust:jcommander:1.48"
    compile 'golem:golem:0.5'
    compile 'io.vertx:vertx-core:3.2.1'
    compile 'io.vertx:vertx-web:3.2.1'

    compile files(findJar("lcm.jar", ["jars/", "/usr/share/java/", "/opt/share/java/", "/usr/local/share/java/"]))
    compile files(findJar("aspn-messages-lcm.jar", ["jars/"]))

    testCompile 'junit:junit:4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

}

// Configure fatJar exclusions
project.ext.fatExcludes = ["native", "jblas"]

String findJar(jarName, paths) {
    def path = paths.find {
        if (file(it+jarName).exists()) {
            return true
        }
        return false
    }
    if (path == null)
        throw new Exception("Can't find $jarName: please supply to build.gradle or put in jars/*")
    else
        return path+jarName
}
